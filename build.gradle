plugins {
    id 'java'
    id 'maven-publish'
    id 'org.graalvm.buildtools.native' version '0.9.19'
}
ext{
    set("springVersion", "6.0.13")
    set("dubboVersion", "3.2.7")
    set("kafkaVersion", "2.8.1")
}
allprojects{
    group = 'com.cheeseocean.im'
    version = '1.0.0'

    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/apache-snapshots' }
        mavenLocal()
        maven {
            url = uri("https://maven.pkg.github.com/cheeseocean/CheesePackages")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
        testCompileOnly 'org.projectlombok:lombok:1.18.26'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

        implementation enforcedPlatform("org.springframework:spring-framework-bom:$springVersion")
        //spring (based ioc aop)
        implementation "org.springframework:spring-context"
        implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
        implementation 'ch.qos.logback:logback-core:1.4.9'
        implementation 'ch.qos.logback:logback-classic:1.4.9'
        implementation 'org.slf4j:log4j-over-slf4j:1.7.25'
        implementation 'org.slf4j:jcl-over-slf4j:1.7.25'

        implementation "org.apache.dubbo:dubbo-config-spring:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-rpc-dubbo:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-remoting-netty4:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-serialization-hessian2:$dubboVersion"
        //nacos-registry
        implementation "org.apache.dubbo:dubbo-registry-nacos:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-configcenter-nacos:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-metadata-report-nacos:$dubboVersion"
        //zookeeper-registry
        implementation "org.apache.dubbo:dubbo-registry-zookeeper:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-configcenter-zookeeper:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-metadata-report-zookeeper:$dubboVersion"
        //lombok
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'
    }

    tasks.register('apiJar', Jar) {
        from(sourceSets.main.output) {
            include 'com/cheeseocean/im/*/api/**'
        }
    }

    tasks.register('apiSourceJar', Jar) {
        from(sourceSets.main.allSource) {
            include 'com/cheeseocean/im/*/api/**'
        }
        archiveClassifier = 'sources'
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/cheeseocean/CheesePackages")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                artifactId "${project.name}-api"
                //using gradle module:publish -Dapi.version or edit in gradle.properties
                version System.getProperty("api.version") ?: project.findProperty("${project.name}.api.version")
                artifact apiJar
                artifact apiSourceJar
                pom {
                    name = "CheeseIM ${project.name} API Jar"
                    description = "CheeseIM API dependencies"
                    url = "http://github.com/cheeseocean/CheeseIM"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "xxxcrel"
                            name = "xxxcrel"
                            email = "xxxcrel@gmail.com"
                        }
                    }
                }
            }
        }
    }

    test {
        useJUnitPlatform()
    }

}