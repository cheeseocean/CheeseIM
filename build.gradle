plugins{
    id 'java'
    id 'maven-publish'
}
ext{
    set("dubboVersion", "3.0.7")
}
allprojects{
    group = 'com.cheeseocean'
    version = '1.0.0'

    repositories {
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        mavenLocal()
        maven {
            url = uri("https://maven.pkg.github.com/cheeseocean/CheesePackages")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
        implementation enforcedPlatform("org.apache.dubbo:dubbo-dependencies-bom:$dubboVersion")
        implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.2'
        implementation 'org.slf4j:slf4j-api:1.7.30'
        implementation 'ch.qos.logback:logback-core:1.2.10'
        implementation 'ch.qos.logback:logback-classic:1.2.10'
        implementation 'org.springframework:spring-context'
        implementation 'com.alibaba.nacos:nacos-client'
        implementation "org.apache.dubbo:dubbo-rpc-dubbo:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-remoting-netty4:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-registry-nacos:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-configcenter-nacos:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-metadata-report-nacos:$dubboVersion"
        implementation "org.apache.dubbo:dubbo-config-spring:$dubboVersion"
    }

    task apiJar (type: Jar){
        from(sourceSets.main.output){
            include 'com/cheeseocean/cheeseim/*/api/**'
        }
    }

    task apiSourceJar(type: Jar){
        from(sourceSets.main.allSource){
            include 'com/cheeseocean/cheeseim/*/api/**'
        }
        archiveClassifier = 'sources'
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/cheeseocean/CheesePackages")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                artifactId "${project.name}-api"
                //using gradle module:publish -Dapi.version or edit in gradle.properties
                version System.getProperty("api.version") ?: project.findProperty("${project.name}.api.version")
                artifact apiJar
                artifact apiSourceJar
                pom {
                    name = "CheeseIM ${project.name} API Jar"
                    description = "CheeseIM API dependencies"
                    url = "http://github.com/cheeseocean/CheeseIM"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "xxxcrel"
                            name = "xxxcrel"
                            email = "xxxcrel@gmail.com"
                        }
                    }
                }
            }
        }
    }

    test {
        useJUnitPlatform()
    }

}